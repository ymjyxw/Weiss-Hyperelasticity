      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,STRAN,DSTRAN,
     2 TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,MATERL,NDI,NSHR,NTENS,
     3 NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,CELENT,
     4 DFGRD0,DFGRD1,NOEL,NPT,KSLAY,KSPT,KSTEP,KINC)
C
      INCLUDE 'ABA_PARAM.INC'
C
      CHARACTER*8 MATERL
      DIMENSION STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),DDSDDT(NTENS),DRPLDE(NTENS),
     2 STRAN(NTENS),DSTRAN(NTENS),DFGRD0(3,3),DFGRD1(3,3),
     3 TIME(2),PREDEF(1),DPRED(1),PROPS(NPROPS),COORDS(3),DROT(3,3)
C ----------------------------------------------------------------
C    BBAR   - DEVIATORIC LEFT CAUCHY-GREEN TENSOR
C    DISTGR - DEVIATORIC DEFORMATION GRADIENT (DISTORTION TENSOR)
C    BBAR = J**(-2/3)B=FBAR * trans(FBAR)
C    DISTGR = FBAR = J**(-1/3)F
C ----------------------------------------------------------------
C
      DIMENSION BBAR(6),DISTGR(3,3),VB(3),VV(6),I2CC(6,6),
     1 STRESSISO(NTENS),BBAR2(6),BBARP(3), BBARN(3,3),
     2 EELAS(6), EELASP(3)
C
      PARAMETER(ZERO=0.D0, ONE=1.D0, TWO=2.D0, THREE=3.D0, FOUR=4.D0,
     1        FIVE=5.D0,SIX=6.D0,SEVEN=7.D0,EIGHT=8.D0,NINE=9.D0,
     2        TEN=10.D0)
      INTEGER LENJOBNAME,LENOUTDIR
      CHARACTER*256 JOBNAME,OUTDIR,FILENAME
      REAL*8 C1,C2,C3,D1,THETA,a01,a02,a03,DET,SCALE,term1,term2,
     1       I1BAR,I2BAR,I4BAR,EK,PR,I2BARTEM,a1,a2,a3
C
C ----------------------------------------------------------------
C    UMAT FOR COMPRESSIBLE Weiss ANISOTROPIC HYPERELASTICITYs
C    STRAIN ENERGY FUNCTION:
C    PSI=C1(I1BAR-3)+C2(I2BAR-3)+C3(EXP(I1BAR-1)-I4BAR)+(1/D)*(J-1)^2
C    CANNOT BE USED FOR PLANE STRESS
C ----------------------------------------------------------------
C    PROPS(1) - C10-C1
C    PROPS(2) - C01-C2
C    PROPS(3) - C3
C    PROPS(4) - D1      
C ----------------------------------------------------------------
C
C    ELASTIC PROPERTIES
C
      C1 = PROPS(1)
      C2 = PROPS(2)
      C3 = PROPS(3)
      D1 = PROPS(4)
C
      THETA=0.0
      a01=COSD(THETA)
      a02=SIND(THETA)
      a03=0.0
C      
C
C    JACOBIAN AND DISTORTION TENSOR
C     DET=J=det(F) SCALE=J**(-1/3)
C
      DET=DFGRD1(1, 1)*DFGRD1(2, 2)*DFGRD1(3, 3)
     1   -DFGRD1(1, 2)*DFGRD1(2, 1)*DFGRD1(3, 3)
      IF(NSHR.EQ.3) THEN
        DET=DET+DFGRD1(1, 2)*DFGRD1(2, 3)*DFGRD1(3, 1)
     1         +DFGRD1(1, 3)*DFGRD1(3, 2)*DFGRD1(2, 1)
     2         -DFGRD1(1, 3)*DFGRD1(3, 1)*DFGRD1(2, 2)
     3         -DFGRD1(2, 3)*DFGRD1(3, 2)*DFGRD1(1, 1)
      END IF
      SCALE=DET**(-ONE/THREE)
      DO k1=1, 3
        DO k2=1, 3
          DISTGR(k1, k2)=SCALE*DFGRD1(k1, k2)
        END DO
      END DO
C
C    CALCULATE DEVIATORIC LEFT CAUCHY-GREEN TENSOR  BBAR = FBAR * trans(FBAR)
C
      BBAR(1)=DISTGR(1, 1)**2+DISTGR(1, 2)**2+DISTGR(1, 3)**2
      BBAR(2)=DISTGR(2, 1)**2+DISTGR(2, 2)**2+DISTGR(2, 3)**2
      BBAR(3)=DISTGR(3, 3)**2+DISTGR(3, 1)**2+DISTGR(3, 2)**2
      BBAR(4)=DISTGR(1, 1)*DISTGR(2, 1)+DISTGR(1, 2)*DISTGR(2, 2)
     1       +DISTGR(1, 3)*DISTGR(2, 3)
      IF(NSHR.EQ.3) THEN
        BBAR(5)=DISTGR(1, 1)*DISTGR(3, 1)+DISTGR(1, 2)*DISTGR(3, 2)
     1         +DISTGR(1, 3)*DISTGR(3, 3)
        BBAR(6)=DISTGR(2, 1)*DISTGR(3, 1)+DISTGR(2, 2)*DISTGR(3, 2)
     1         +DISTGR(2, 3)*DISTGR(3, 3)
      END IF 
C     BBAR2=BBAR*BBAR
      BBAR2(1)=BBAR(1)*BBAR(1)+BBAR(4)*BBAR(4)+BBAR(5)*BBAR(5)
      BBAR2(2)=BBAR(2)*BBAR(2)+BBAR(4)*BBAR(4)+BBAR(6)*BBAR(6)
      BBAR2(3)=BBAR(3)*BBAR(3)+BBAR(5)*BBAR(5)+BBAR(6)*BBAR(6)
      BBAR2(4)=BBAR(1)*BBAR(4)+BBAR(2)*BBAR(4)+BBAR(5)*BBAR(6)
      IF(NSHR.EQ.3) THEN
          BBAR2(5)=BBAR(1)*BBAR(5)+BBAR(4)*BBAR(6)+BBAR(5)*BBAR(3)
          BBAR2(6)=BBAR(4)*BBAR(5)+BBAR(2)*BBAR(6)+BBAR(6)*BBAR(3)
      END IF 
C
C       CALCULATE FIBER ORIENTATION IN CURRENT CONFIGURATIION
C       VB: FIBER ORIENTATION AFTER DEFORMATION     
C       a: UNIT FIBER ORIENTATION AFTER DEFORMATION
C
      VB(1)=DFGRD1(1,1)*a01+DFGRD1(1,2)*a02+DFGRD1(1,3)*a03
      VB(2)=DFGRD1(2,1)*a01+DFGRD1(2,2)*a02+DFGRD1(2,3)*a03
      VB(3)=DFGRD1(3,1)*a01+DFGRD1(3,2)*a02+DFGRD1(3,3)*a03
C
C     CALCULATE INVARIANT I1,I2&I4 
C
      I1BAR=BBAR(1)+BBAR(2)+BBAR(3)
      I2BARTEM=BBAR2(1)+BBAR2(2)+BBAR2(3)
      I2BAR=(I1BAR*I1BAR-I2BARTEM)/TWO
      I4BAR=VB(1)*VB(1)+VB(2)*VB(2)+VB(3)*VB(3)
      a1=VB(1)/SQRT(I4BAR)
      a2=VB(2)/SQRT(I4BAR)
      a3=VB(3)/SQRT(I4BAR)
      VV(1)=a1*a1
      VV(2)=a2*a2
      VV(3)=a3*a3
      VV(4)=a1*a2
      VV(5)=a1*a3
      VV(6)=a2*a3
C      write(100,*) a(1),a(2),a(3)
C
C    CALCULATE THE STRESS
C
      EG=TWO/DET
      TERM2=EXP(I4BAR-ONE)
      TERM1=TERM2-ONE
      EK=TWO/D1*(TWO*DET-ONE)
      PR=TWO/D1*(DET-ONE)
      DO i=1,NDI
        STRESSISO(i)=EG*((C1+C2*I1BAR)*BBAR(i)-C2*BBAR2(i)
     1   +C3*TERM1*I4BAR*VV(i)-ONE/THREE*(C1*I1BAR+2*C2*I2BAR
     2   +C3*TERM1*I4BAR))
        STRESS(i)=STRESSISO(i)+PR
      END DO
      DO i=NDI+1,NDI+NSHR
        STRESSISO(i)=EG*((C1+C2*I1BAR)*BBAR(i)-C2*BBAR2(i)
     1   +C3*TERM1*I4BAR*VV(i))
        STRESS(i)=STRESSISO(i)
      END DO
C
C    CALCULATE THE STIFFNESS
C
      EG13=EG*ONE/THREE
      EG23=EG*TWO/THREE
      EG43=EG*FOUR/THREE
      EG83=EG*EIGHT/THREE
      EG29=EG*TWO/NINE
      EG2=EG*TWO
      DDSDDE(1, 1)= -EG43*(C1+TWO*C2*I1BAR)*BBAR(1)
     1 +EG83*C2*BBAR2(1)+EG29*(FOUR*C1*I1BAR+TEN*C2*I2BAR+
     2 FOUR*C3*I4BAR*TERM1+C3*I4BAR*I4BAR*TERM2)-
     3 EG43*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(1)+
     4 EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(1)+TWO*STRESSISO(1)+EK
      DDSDDE(2, 2)= -EG43*(C1+TWO*C2*I1BAR)*BBAR(2)+
     1 EG83*C2*BBAR2(2)+EG29*(FOUR*C1*I1BAR+TEN*C2*I2BAR+
     2 FOUR*C3*I4BAR*TERM1+C3*I4BAR*I4BAR*TERM2)-
     3 EG43*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(2)+
     4 EG2*C3*I4BAR*I4BAR*TERM2*VV(2)*VV(2)+TWO*STRESSISO(2)+EK
      DDSDDE(3, 3)= -EG43*(C1+TWO*C2*I1BAR)*BBAR(3)+
     1 EG83*C2*BBAR2(3)+EG29*(FOUR*C1*I1BAR+TEN*C2*I2BAR+
     2 FOUR*C3*I4BAR*TERM1+C3*I4BAR*I4BAR*TERM2)-
     3 EG43*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(3)+
     4 EG2*C3*I4BAR*I4BAR*TERM2*VV(3)*VV(3)+TWO*STRESSISO(3)+EK
      DDSDDE(1, 2)= EG2*C2*(BBAR(1)*BBAR(2)-BBAR(4)*BBAR(4))-
     1 EG23*(C1+TWO*C2*I1BAR)*(BBAR(1)+BBAR(2))+
     2 EG43*C2*(BBAR2(1)+BBAR2(2))+
     3 EG29*(C1*I1BAR+FOUR*C2*I2BAR+C3*I4BAR*TERM1+
     4 C3*I4BAR*I4BAR*TERM2)-
     5 EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*(VV(1)+VV(2))+
     6 EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(2)+EK
      DDSDDE(1, 3)=EG2*C2*(BBAR(1)*BBAR(3)-BBAR(5)*BBAR(5))-
     1 EG23*(C1+TWO*C2*I1BAR)*(BBAR(1)+BBAR(3))+
     2 EG43*C2*(BBAR2(1)+BBAR2(3))+
     3 EG29*(C1*I1BAR+FOUR*C2*I2BAR+C3*I4BAR*TERM1+
     4 C3*I4BAR*I4BAR*TERM2)-
     5 EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*(VV(1)+VV(3))+
     6 EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(3)+EK
      DDSDDE(2, 3)=EG2*C2*(BBAR(2)*BBAR(3)-BBAR(6)*BBAR(6))-
     1 EG23*(C1+TWO*C2*I1BAR)*(BBAR(2)+BBAR(3))+
     2 EG43*C2*(BBAR2(2)+BBAR2(3))+
     d EG29*(C1*I1BAR+FOUR*C2*I2BAR+C3*I4BAR*TERM1+
     4 C3*I4BAR*I4BAR*TERM2)-
     5 EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*(VV(2)+VV(3))+
     6 EG2*C3*I4BAR*I4BAR*TERM2*VV(2)*VV(3)+EK
      DDSDDE(1, 4)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(4)+
     1 EG43*C2*BBAR2(4)+
     2 -EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(4)
     3 +EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(4)+STRESSISO(4)
      DDSDDE(2, 4)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(4)+
     1 EG43*C2*BBAR2(4)+
     2 -EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(4)
     3 +EG2*C3*I4BAR*I4BAR*TERM2*VV(2)*VV(4)+STRESSISO(4)
      DDSDDE(3, 4)= EG2*C2*(BBAR(3)*BBAR(4)-BBAR(5)*BBAR(6))-
     1 EG23*(C1+TWO*C2*I1BAR)*BBAR(4)+EG43*C2*BBAR2(4)-
     2 EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(4)+
     3 EG2*C3*I4BAR*I4BAR*TERM2*VV(3)*VV(4)
      DDSDDE(4, 4)= EG2*C2*(BBAR(4)*BBAR(4)-
     1 (BBAR(1)*BBAR(2)+BBAR(4)*BBAR(4))/TWO)+
     2 EG13*(C1*I1BAR+TWO*C2*I2BAR+C3*I4BAR*TERM1)+
     3 EG2*C3*I4BAR*I4BAR*TERM2*VV(4)*VV(4)+
     4 (STRESSISO(1)+STRESSISO(2))/TWO
      IF(NSHR.EQ.3) THEN
        DDSDDE(1, 5)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(5)+
     1  EG43*C2*BBAR2(5)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(5)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(5)+STRESSISO(5)
        DDSDDE(2, 5)=EG2*C2*(BBAR(2)*BBAR(5)-BBAR(4)*BBAR(6))-
     1  EG23*(C1+TWO*C2*I1BAR)*BBAR(5)+EG43*C2*BBAR2(5)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(5)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(2)*VV(5)
        DDSDDE(3, 5)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(5)+
     1  EG43*C2*BBAR2(5)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(5)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(3)*VV(5)+STRESSISO(5)
        DDSDDE(4, 5)=EG2*C2*(BBAR(4)*BBAR(5)-
     1  (BBAR(1)*BBAR(6)+BBAR(5)*BBAR(4))/TWO)+
     2  EG2*C3*I4BAR*I4BAR*TERM2*VV(4)*VV(5)+STRESSISO(6)/TWO
        DDSDDE(5, 5)= EG2*C2*(BBAR(5)*BBAR(5)-
     1  (BBAR(1)*BBAR(3)+BBAR(5)*BBAR(5))/TWO)+
     2  EG13*(C1*I1BAR+TWO*C2*I2BAR+C3*I4BAR*TERM1)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(5)*VV(5)+
     4  (STRESSISO(1)+STRESSISO(3))/TWO
        DDSDDE(1, 6)= EG2*C2*(BBAR(1)*BBAR(6)-BBAR(4)*BBAR(5))-
     1  EG23*(C1+TWO*C2*I1BAR)*BBAR(6)+EG43*C2*BBAR2(6)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(6)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(1)*VV(6)
        DDSDDE(2, 6)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(6)+
     1  EG43*C2*BBAR2(6)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(6)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(2)*VV(6)+STRESSISO(6)
        DDSDDE(3, 6)= -EG23*(C1+TWO*C2*I1BAR)*BBAR(6)+
     1  EG43*C2*BBAR2(6)-
     2  EG23*C3*I4BAR*((I4BAR+ONE)*TERM2-ONE)*VV(6)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(3)*VV(6)+STRESSISO(6)
        DDSDDE(4,6)= EG2*C2*(BBAR(4)*BBAR(6)-
     1  (BBAR(4)*BBAR(6)+BBAR(5)*BBAR(2))/TWO)+
     2  EG2*C3*I4BAR*I4BAR*TERM2*VV(4)*VV(6)+STRESSISO(5)/TWO
        DDSDDE(5,6)= EG2*C2*(BBAR(5)*BBAR(6)-
     1  (BBAR(4)*BBAR(3)+BBAR(5)*BBAR(6))/TWO)+
     2  EG2*C3*I4BAR*I4BAR*TERM2*VV(5)*VV(6)+STRESSISO(4)/TWO
        DDSDDE(6,6)= EG2*C2*(BBAR(6)*BBAR(6)-
     1  (BBAR(2)*BBAR(3)+BBAR(6)*BBAR(6))/TWO)+
     2  EG13*(C1*I1BAR+TWO*C2*I2BAR+C3*I4BAR*TERM1)+
     3  EG2*C3*I4BAR*I4BAR*TERM2*VV(6)*VV(6)+
     4  (STRESSISO(2)+STRESSISO(3))/TWO
      END IF
      DO K1=1, NTENS
        DO K2=1, K1-1
          DDSDDE(K1, K2)=DDSDDE(K2, K1)
        END DO
      END DO
C
C     CALCULATE LOGARITHMIC ELASTIC STRAINS (OPTIONAL)
C
      CALL SPRIND (BBAR, BBARP, BBARN, 1, NDI, NSHR)
      CALL SPRIND (ss, sst, ssp, 1, NDI, NSHR)

      EELASP(1) = LOG(SQRT(BBARP(1))/SCALE)
      EELASP(2) = LOG(SQRT(BBARP(2))/SCALE)
      EELASP(3) = LOG(SQRT(BBARP(3))/SCALE)

      EELAS(1) = EELASP(1) * BBARN(1,1)**2 + EELASP(2) * BBARN(2,1)**2
     1         + EELASP(3) * BBARN(3,1)**2
      EELAS(2) = EELASP(1) * BBARN(1,2)**2 + EELASP(2) * BBARN(2,2)**2
     1         + EELASP(3) * BBARN(3,2)**2
      EELAS(3) = EELASP(1) * BBARN(1,3)**2 + EELASP(2) * BBARN(2,3)**2
     1         + EELASP(3) * BBARN(3,3)**2
      EELAS(4) = TWO * (EELASP(1) * BBARN(1,1) * BBARN(1,2)
     1               +  EELASP(2) * BBARN(2,1) * BBARN(2,2)
     2               +  EELASP(3) * BBARN(3,1) * BBARN(3,2))
      IF (NSHR .EQ. 3) THEN
        EELAS(5) = TWO * (EELASP(1) * BBARN(1,1) * BBARN(1,3)
     1                 +  EELASP(2) * BBARN(2,1) * BBARN(2,3)
     2                 +  EELASP(3) * BBARN(3,1) * BBARN(3,3))
        EELAS(6) = TWO * (EELASP(1) * BBARN(1,2) * BBARN(1,3)
     1                 +  EELASP(2) * BBARN(2,2) * BBARN(2,3)
     2                 +  EELASP(3) * BBARN(3,2) * BBARN(3,3))
      END IF
C
C     STORE ELASTIC STRAINS IN STATE VARIABLE ARRAY
C
      DO K1 = 1, NTENS
        STATEV(K1) = EELAS(K1)
      END DO
C
      RETURN
      END
